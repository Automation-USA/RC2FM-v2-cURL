# Name:
[GET]__RingCentral-Account-Info-()

History:
Created by: Julio Toledo - Automation USA LLC
Created: 2020-01-11
Last Modified: 2023-1111 - Julio Toledo
Completed code QA

Purpose:
Get Basic Account Info

Parameters: None

Global Variables:
-Requires valid "$$rc.responseTokenOAuth" JSON object (from API session authentication)
-Requires "$$rc.tokenTTL" API environment variable 

Reference:
https://developers.ringcentral.com/api-reference/account

Important Notes:
-Requires FileMaker 16+ native JSON functions

Set Variable [ $$rc.responseAccount ; Value: "" ] 
Set Variable [ $scriptName ; Value: "Script: " & Quote ( Get (ScriptName) ) ] 
# 
# validate token
Set Variable [ $tokenTimeLeft ; Value: GetAsNumber ( GetAsTimestamp ( $$rc.accessTokenExpiryTime )  - Get ( CurrentHostTimestamp ) ) ] 
If [ $tokenTimeLeft < 60 ] 
	Perform Script [ Specified: From list ; “[POST]__RingCentral-Refresh-Token-[Prolong-Session]-( )” ; Parameter:    ]
End If
Set Variable [ $token ; Value: JSONGetElement ( $$rc.responseTokenOAuth ; ".access_token" ) ] 
# 
# concatenate entire cURL command
Set Variable [ $cURL ; Value: " -X GET --header \"Accept: application/json\" --header \"Authorization: Bearer " & $token & "\" --header \"User-Agent: " & $$rc.userAgent & "\" --dump-header \"$$rc.responseAccountHeader\"" ] 
# 
# concatenate endpoint
Set Variable [ $url ; Value: $$rc.baseURL & $$rc.endpointAccount ] 
# 
# [HTTP-GET]
Insert from URL [ Select ; With dialog: Off ; Target: $$rc.responseAccount ; $url ; Verify SSL Certificates ; cURL options: $cURL ] 
# 
# Parse response
Perform Script [ Specified: From list ; “[Function]__JSONparse__RC.AccountInfo($$rc.responseAccount)” ; Parameter:    ]
